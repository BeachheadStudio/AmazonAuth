// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:1.5.0'

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

allprojects {
    repositories {
        jcenter()
    }

    tasks.withType(JavaCompile) {
        options.encoding = "UTF-8"
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir

    delete fileTree('Assets/Plugins/Android/Amazon/libs') {
        include "**/*.jar"
    }

    delete 'Assets/Plugins/Android/Amazon/AndroidManifest.xml'
    delete 'Assets/Plugins/Android/Amazon/project.properties'

    println 'clean: done'
}

//noinspection GroovyAssignabilityCheck
task makeUnityPlugin(dependsOn: ':authservice:build') {
    doLast {
        println 'Building Unity plugin'

        copy {
            from 'AmazonInsights-android-sdk/AmazonInsights-android-sdk.jar'
            into 'Assets/Plugins/Android/Amazon/libs'
        }

        copy {
            from 'gamecirclesdk/gamecirclesdk.jar'
            into 'Assets/Plugins/Android/Amazon/libs'
        }

        copy {
            from 'login-with-amazon-sdk/login-with-amazon-sdk.jar'
            into 'Assets/Plugins/Android/Amazon/libs'
        }

        copy {
            from('authservice/build/intermediates/bundles/release') {
                include 'classes.jar'
                rename 'classes.jar', 'amazonauth.jar'
            }
            into 'Assets/Plugins/Android/Amazon/libs'
        }

        copy {
            from('authservice/src/main/res') {
                include '**/*.*'
            }
            into 'Assets/Plugins/Android/Amazon/res'
        }

        copy {
            from('authservice/') {
                include 'project.properties'
            }
            into 'Assets/Plugins/Android/Amazon'
        }

        copy {
            from('authservice/src/main') {
                include 'AndroidManifest.xml'
            }
            into 'Assets/Plugins/Android/Amazon'
        }

        println 'Done'
    }
}